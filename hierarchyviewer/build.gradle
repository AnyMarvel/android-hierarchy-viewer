apply plugin: 'com.android.library'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    signingConfigs {
        hierarchyViewerConfig {
            storeFile file("${rootDir}/dev_tools/hierarchy_viewer.keystore")
            storePassword "*******"
            keyAlias "*******"
            keyPassword "*******"
        }
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "SNAPSHOT-1.0"
    }
    buildTypes {
        debug {
            buildConfigField 'int', 'PORT', '4000'
        }
        release {
            buildConfigField 'int', 'PORT', '4000'
            minifyEnabled false
            signingConfig android.signingConfigs.hierarchyViewerConfig
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

android.libraryVariants.all { variant ->
    def buildWebappTask = tasks.create(name: "buildWebappFor${variant.name.capitalize()}", type: Exec) {
        onlyIf {
            project.properties["buildWebapp"]
        }
        description "Builds webapp content with grunt"
        doFirst {
            println '''
               ______                 __
              / ____/______  ______  / /_
             / / __/ ___/ / / / __ \\/ __/
            / /_/ / /  / /_/ / / / / /_
            \\____/_/   \\__,_/_/ /_/\\__/

            '''
        }
        doLast {
            println '''
              ________      ____  ____
             /_  __/ /   _ / __ \\/ __ \\
              / / / /   (_) / / / /_/ /
             / / / /____ / /_/ / _, _/    Grunt is done.
            /_/ /_____( )_____/_/ |_|
                      |/
            '''
        }
        workingDir rootDir
        commandLine "grunt", "build"
    }

    project.tasks["process${variant.name.capitalize()}Resources"].dependsOn buildWebappTask
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.nanohttpd:nanohttpd:2.1.1'
    compile 'com.google.dagger:dagger:2.0-SNAPSHOT'
    compile 'org.immutables:gson:1.1.3'

    apt 'com.google.dagger:dagger-compiler:2.0-SNAPSHOT'

    provided 'org.glassfish:javax.annotation:10.0-b28'
}
